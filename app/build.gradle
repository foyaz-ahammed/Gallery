apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 29
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "com.kr.gallery.pro"
        minSdkVersion propMinSdkVersion
        targetSdkVersion 27                     // shared transition completely works <= 27
        versionCode 1
        versionName "1.0.0"
        multiDexEnabled true
        setProperty("archivesBaseName", "gallery-$versionCode")
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        debug {
            // we cannot change the original package name, else PhotoEditorSDK won't work
            //applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        if (is_proprietary) {
            main.java.srcDirs += 'src/proprietary/kotlin'
        }
    }

    flavorDimensions "licensing"
    productFlavors {

        kr {}
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    packagingOptions {
        exclude 'META-INF/library_release.kotlin_module'
    }
}

dependencies {
    //Leak canary plugin추가
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.5'

    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'it.sephiroth.android.exif:library:1.0.1'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.19'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'
    implementation 'com.google.vr:sdk-panowidget:1.180.0'
    implementation 'com.google.vr:sdk-videowidget:1.180.0'
    implementation 'org.apache.sanselan:sanselan:0.97-incubator'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.wang.avi:library:2.1.3'
    implementation 'com.caverock:androidsvg-aar:1.3'
    implementation 'com.github.tibbi:subsampling-scale-image-view:81c021514c'
    implementation project(':indicatorseekbar')
    implementation project(':playerbase')
    implementation project(':gpu-image-plus')

    implementation "com.github.skydoves:powerspinner:1.1.7"

    //image slider library
    implementation project(':autoimageslider')

    implementation project(':commons')

    kapt 'androidx.room:room-compiler:2.2.5'
    implementation 'androidx.room:room-runtime:2.2.5'
    annotationProcessor 'androidx.room:room-compiler:2.2.5'
    implementation "androidx.room:room-ktx:2.2.5"
    implementation "androidx.core:core-ktx:1.3.2"
    implementation 'androidx.viewpager2:viewpager2:1.0.0-beta03'
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"
    implementation 'androidx.fragment:fragment-ktx:1.2.5'
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"

    implementation 'com.github.1993hzw:Androids:1.3'
    implementation "com.google.android.material:material:1.1.0-alpha07"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

// Apply the PESDKPlugin
if (is_proprietary) {
    apply plugin: 'ly.img.android.sdk'

    imglyConfig {
        pesdk {
            enabled true
            licencePath 'pesdk_license'
        }

        modules {
            include 'ui:core'
            include 'ui:text'
            include 'ui:focus'
            include 'ui:brush'
            include 'ui:filter'
            include 'ui:transform'
            include 'ui:adjustment'

            include 'backend:serializer'

            include 'assets:font-basic'
            include 'assets:filter-basic'
        }
    }
}
repositories {
    mavenCentral()
}
